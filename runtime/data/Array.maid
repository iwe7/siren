module | root vm | where

vm define-global: "Array" as: { vm native-array };

vm apply-trait-globally: extend Array with {
  def as-string
  "[" + (this join: ", ") + "]"

  (* Accessing *)
  def at: index
  vm array: this at: index

  def at: index put: value
  this copy at: index put!: value
  
  def at: index put!: value
  vm array: this at: index put: value;
  this

  def remove-at: index
  this copy remove-at!: index
  
  def remove-at!: index
  vm array: this remove-at: index;
  this
  
  (* Collection operations *)
  def length
  vm array/length: this

  def + that
  vm array: this concat: that

  def index-of: value
  let index = vm array: this index-of: value;
  (index === 0) then: { Result failure: "Not found." }
                else: { Result ok: index }

  def last-index-of: value
  let index = vm array: this last-index-of: value;
  (index === 0) then: { Result failure: "Not found." }
                else: { Result ok: index }

  def join: separator
  vm array: (this map: _ as-string) join: separator

  def copy
  vm array/shallow-copy: this

  (* Iteration *)
  def each: computation
  vm array: this each: { a | computation call: a };
  this

  def map: computation
  vm array: this map: { a | computation call: a }

  def filter: predicate
  vm array: this filter: { a | (predicate call: a) then: { vm true } else: { vm false } }
  
  (* Mutation *)
  def append: value
  this copy push!: value

  def append!: value
  vm array: this push: value;
  this

  def without-last
  this copy without-last!

  def without-last!
  vm array/pop: this;
  this

  def prepend: value
  this copy prepend!: value

  def prepend!: value
  vm array: this shift: value;
  this

  def reverse
  this copy reverse!

  def reverse!
  vm array/reverse: this;
  this

  def sort-by: comparator
  this copy sort-by!: coparator

  def sort-by!: comparator
  vm array: this sort: { left right |
    comparator call: left with: right >> match: {
      def LESS    { -1 }
      def EQUAL   {  0 }
      def GREATER {  1 }
    }
  };
  this
  
}