(**********************************************************************
 *
 * This source file is part of the Siren project.
 *
 * Copyright (C) 2013-2015 Quildreen Motta.
 * Licensed under the MIT licence.
 *
 * See LICENCE for licence information.
 * See CONTRIBUTORS for the list of contributors to the project.
 *
 **********************************************************************)
module | vm | where

vm define-global: "Result" as: { Result };
let Result = {
  def this traits = {
    def _ Ok = Ok
    def _ Failure = Failure
  }

  def this ok: value
    this traits Ok {
      def _ value = value
    }

  def this failure: value
    this traits Failure {
      def _ value = value
    }

  def this merge
    this value
};

let Ok = Result {
  def this map: transformation
    let newValue = transformation call: this value;
    this { def _ value newValue }

  def this chain: transformation
    transformation call: this value

  def this match: pattern
    pattern ok: this value

  def this recover: handler
    this

  def this as-string
    "<Result ok: " + this value as-string + ">"

  def this as-tuple
    [this value]

  def this === aResult
    let value = this value;
    aResult match: {
      def _ ok: v      = v === value
      def _ failure: v = False
    }

  def this ok?
    True

  def this failure?
    False

  def this then: ok else: failure
    ok value

  def this get!
    this value

  def this get-or-default: defaultValue
    this value
};

let Failure = Result {
  def this map: transformation
    this

  def this chain: transformation
    this

  def this match: pattern
    pattern failure: this value

  def this recover: handler
    handler call: this value

  def this as-string
    "<Result failure: " + this value as-string + ">"

  def this as-tuple
    []

  def this === aResult
    let value = this value;
    aResult match: {
      def _ ok: v      = False
      def _ failure: v = v === value
    }

  def this ok?
    False

  def this failure?
    True

  def this then: ok else: failure
    failure value

  def this get!
    Error panic!: "Can't extract a value from a failure. Use `merge`, `get-or-default:`, or `recover:`"

  def this get-or-default: defaultValue
    defaultValue
}