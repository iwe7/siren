#-----------------------------------------------------------------------
# 
# This source file is part of the Siren project.
#
# Copyright (C) 2013-2015 Quildreen Motta.
# Licensed under the MIT licence.
#
# See LICENCE for licence information.
# See CONTRIBUTORS for the list of contributors to the project.
#
#-----------------------------------------------------------------------
$siren/1
module | vm | where

vm extend: Root with: {
  def_ Console = Console.
}.

let Console = {
  def self describe
    Debug-Text, "<Console>".

  def self write: text
    vm assert/text: text.
    Task new: { resolver |
      vm console/write: text.
      resolver success: Unit.
    }

  def self write!: text
    self write: text; run!; future.

  def self write-line: text
    self write: text, "\n"

  def self write-line!: text
    self write-line: text; run!; future.

  def self write-error: text
    vm assert/text: text.
    Task new: { resolver |
      vm console/error: text.
      resolver success: Unit.
    }

  def self write-error!: text
    self write-error: text; run!; future.
}