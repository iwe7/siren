#-----------------------------------------------------------------------
# 
# This source file is part of the Siren project.
#
# Copyright (C) 2013-2015 Quildreen Motta.
# Licensed under the MIT licence.
#
# See LICENCE for licence information.
# See CONTRIBUTORS for the list of contributors to the project.
#
#-----------------------------------------------------------------------
module | vm | where

vm define-global: "Boolean" as: { Boolean };
let Boolean = { };

vm define-global: "True" as: { True };
let True = Boolean {
  def this as-string
    # A textual representation of the object. 
    "True"

  def this && that
    # Logical conjunction 
    that

  def this || that
    # Logical disjunction 
    this

  def this not
    # Logical negation 
    False

  def this then: consequent else: alternate
    # Branches in two expressions, depending on the value of the boolean.
    #
    # If `True`, executes the consequent branch. Otherwise executes the
    # `alternate` branch. 
    consequent value

  def this otherwise: alternate
    # Executes a block if the receiver is `False`. 
    Unit

  def this then: consequent
    # Executes a block if the receiver is `True` 
    consequent value;
    Unit
};

vm define-global: "False" as: { False };
let False = Boolean {
  def this as-string
    # Return a textual representation of the object. 
    "False"

  def this && that
    # Logical conjunction 
    this

  def this || that
    # Logical disjunction 
    that

  def this not
    # Logical negation 
    True

  def this then: consequent else: alternate
    # Branches in two expressions, depending on the value of the boolean.
    #
    # If `True`, executes the consequent branch. Otherwise executes the
    # `alternate` branch. 
    alternate value

  def this then: consequent
    # Executes a block if the receiver is `True` 
    Unit

  def this otherwise: alternate
    # Executes a block if the receiver is `False`. 
    alternate value;
    Unit
};

vm foreign/convert: True as: { _ | vm true };
vm foreign/convert: False as: { _ | vm false }