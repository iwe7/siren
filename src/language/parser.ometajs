// # module: siren.parser
//
// Parser for the Siren language.

// -- Dependencies -----------------------------------------------------
var ast = require('./ast');
var extend = require('xtend');
var Expr = ast.Expr;
var Do = ast.DoClause;

var reserved = [
  'extend', 'let', 'do', 'module', 'exposing', 'def',
  '|', '<-', '=', '>>', 'with', 'where', 'use', 'in', 'using', 'return',
  'trait'
]

function isValid(a) {
  return reserved.indexOf(a) === -1
}

function get(k){ return function(o){
  return o[k]
}}

function keywordApply(meta, target, args) {
  var id = Expr.Id(meta, args.map(get(0)).map(get('name')).join(''));
  return Expr.Apply(meta, id, target, args.map(get(1)))
}

function keywordLambda(meta, self, args, body, docs) {
  var id = Expr.Id(meta, args.map(get(0)).map(get('name')).join(''));
  return [id, Expr.Lambda(extend(meta, { docs: docs }), self, args.map(get(1)), body)]
}

function decorateLet(letNode, expr) {
  return Expr.Let(expr.meta,
                  letNode.name,
                  Expr.Apply(letNode.meta, Expr.Id({}, 'call:'), expr, [letNode.value]))
}

// ## object: SirenParser
ometa SirenParser {
  space   = /\s/,
  ignored = space | comment,
  comment = fromTo('#', '\n'):a -> a.slice(1),
  ws      = ignored*,
  sep     = ',' | '.',
  symbol  = ~( letter | digit | space
             | '_' | '$' | '^' | '@' | ';' | ':' | '{' | '}' | '(' | ')' | '[' | ']'
             ) char,
  eof     = ~char,
  kw :xs  = seq(xs) ~(letter | digit | symbol | sep | ':'),


  // Identifiers
  idStart    = letter,
  idRest     = ~sep (letter | digit | symbol),
  identifier = <idStart idRest*>:a     ?(isValid(a)) -> Expr.Id({}, a),
  keyword    = <idStart idRest*>:a ':'               -> Expr.Id({}, a + ':'),
  symbols    = <symbol+>:a             ?(isValid(a)) -> Expr.Id({}, a),
  argName    = identifier:a ~':' -> a
             | '_'               -> Expr.Id({}, '_'),

  selector   = identifier
             | symbols
             | keyword+:xs -> Expr.Id(xs[0].meta, xs.map(get('name')).join('')),

  // Patterns
  pattern = decoratedPattern
          | kw('def') ws (keywordPattern | binaryPattern | unaryPattern),

  decoratedPattern = '@' expr:a ws pattern:p
                  -> [p[0], Expr.Apply({}, Expr.Id({}, 'call:'), a, [p[1]])],

  docComment  = (space* comment)+:as ws -> as.join('\n').trim(),
  patternBody = docComment?:a ws ('=' ws)? stmtList:as -> [as, a || ''],

  keywordArgs = (ws keyword:k ws argName:n -> [k, n])+,

  keywordPattern = argName:a space* keywordArgs:as space* patternBody:v
                -> keywordLambda({}, a, as, v[0], v[1]),
  unaryPattern   = argName:a space* argName:b space* patternBody:v
                -> [b, Expr.Lambda({ docs: v[1] }, a, [], v[0])],
  binaryPattern  = argName:a space* symbols:b ws argName:c space* patternBody:v
                -> [b, Expr.Lambda({ docs: v[1] }, a, [c], v[0])],



  // Statements
  letStmt = '@' expr:a ws letStmt:b             -> decorateLet(b, a)
          | kw('let') ws identifier:a ~':' ws '=' ws expr:b -> Expr.Let({}, a, b),

  module     = kw('module') ws moduleArgs?:a ws exportStmt?:b ws kw('where') ws stmtList:cs
             -> Expr.Module({}, a || [], b || Expr.Record({}, []), cs),
  moduleArgs = '|' ws argName:a (ws argName)*:as ws '|' -> [a].concat(as),
  exportStmt = kw('exposing') ws record:a -> a,

  useExpr = kw('use') ws listOf(#expr, ';'):xs ws kw('in') ws '{' ws stmtList:ys ws '}'
          -> Expr.Use({}, xs, ys),

  usingStmt = kw('using') ws listOf(#expr, ';'):xs -> Expr.Using({}, xs),

  // Expressions
  stmt = letStmt
       | usingStmt
       | ';'        -> Expr.Empty
       | expr,

  expr = useExpr
       | pipeExpr,

  pipeExpr = pipeExpr:a ws ">>" ws identifier:i ~':'        -> Expr.Apply({}, i, a, [])
           | pipeExpr:a ws ">>" ws symbols:s ws unaryExpr:b -> Expr.Apply({}, s, a, [b])
           | pipeExpr:a ws ">>" (ws keywordApp)+:as         -> keywordApply({}, a, as)
           | appExpr,

  appExpr = keywordExpr
          | binaryExpr,

  keywordApp  = keyword:k ws binaryExpr:v -> [k, v],

  keywordExpr = binaryExpr:a (ws keywordApp)+:as          -> keywordApply({}, a, as),
  binaryExpr  = binaryExpr:a ws symbols:s ws unaryExpr:b  -> Expr.Apply({}, s, a, [b])
              | unaryExpr,
  unaryExpr   = unaryExpr:a ws identifier:i ~':'      -> Expr.Apply({}, i, a, [])
              | unaryExpr:a ws record:v               -> Expr.Clone({}, a, v)
              | primaryExpr,
  primaryExpr = '_'                                -> Expr.Hole({})
              | doExpr
              | value
              | identifier:a ~':'                  -> Expr.Free({}, a)
              | keywordApp:a (ws keywordApp)*:as   -> keywordApply({}, Expr.GlobalObject({}), [a].concat(as))
              | extendExpr:a                       -> a
              | '(' ws expr:a ws ')'               -> a,

  extendExpr = kw('extend') ws expr:a ws kw('with') ws record:v -> Expr.Extend({}, a, v),

  stmtList = stmt:a (ws ';' ws stmt)*:as -> [a].concat(as),

  blockStmt = stmt
            | '^' ws expr:a -> Expr.Return({}, a),
  blockStmtList = blockStmt:a (ws ';' ws blockStmt)*:as -> [a].concat(as),

  doExpr = kw('do') ws '{' ws (doCase:c ws ';' ws -> c)*:xs ws doTailCase:x ws '}'
         -> Expr.Do({}, xs.concat([x])),
  doAction     = argName:i ws "<-" ws expr:e -> Do.Action({}, i, e)
               | doTailAction,
  doTailAction = expr:e                         -> Do.Action({}, Expr.Id({}, '_'), e),
  doPure       = argName:i ws "<-" ws kw('return') ws expr:e -> Do.Return({}, i, e)
               | doTailPure,
  doTailPure   = kw('return') ws expr:e                         -> Do.Return({},
                                                                             Expr.Id({}, '_'),
                                                                             e),
  doCase       = doPure | doAction,
  doTailCase   = doTailPure | doTailAction,

  // Values
  value = number | string | vector | lambda | record,

  octDigit = /[0-7_]/,
  hexDigit = /[0-9a-fA-F_]/,
  binDigit = '0' | '1' | '_',
  digits = (digit | '_')+:as -> Number(as.join('').replace(/_/g, '')),
  number = '-'?:s ( '0' 'b' <binDigit+>:n
                    -> Expr.Int({}, (s || '+') + parseInt(n.replace(/_/g, ''), 2).toString())
                  | '0' 'o' <octDigit+>:n
                    -> Expr.Int({}, (s || '+') + parseInt(n.replace(/_/g, ''), 8).toString())
                  | '0' 'x' <hexDigit+>:n
                    -> Expr.Int({}, (s || '+') + parseInt(n.replace(/_/g, ''), 16).toString())
                  | digits:a '.' digits:b exponent?:c
                    -> Expr.Num({}, +((s || '+') + a + '.' + b + (c || 0)))
                  | digits:a
                    -> Expr.Int({}, s || '+', String(a))
                  ),
  sign = '+' | '-',
  exponent = ('e' | 'E') sign?:s digits:d -> s + d,

  stringEscape     = '\\' ( 'b' -> '\b'
                          | 'f' -> '\f'
                          | 'n' -> '\n'
                          | 'r' -> '\r'
                          | 't' -> '\t'
                          | char),
  stringChar       = (stringEscape | (~seq('"') char)):a -> a,
  unescapableChars = ~seq('"""') char,
  string           = seq('"""') <unescapableChars*>:as seq('"""') -> Expr.Str({}, as)
                   | '"' (stringChar)*:as '"'                     -> Expr.Str({}, as.join('')),

  vector = '[' ws expr:hd (ws ';' ws expr)*:tl ws ']' -> Expr.Vector({}, [hd].concat(tl))
         | '[' ws ']'                                 -> Expr.Vector({}, []),

  record = '{' ws pattern:hd (ws pattern)*:tl ws '}' -> Expr.Record({}, [hd].concat(tl))
         | '{' ws '}'                                -> Expr.Record({}, []),

  lambda  = '{' ws arglist:as ws '|' ws blockStmtList:a ws '}' -> Expr.Block({}, as, a)
          | '{' ws blockStmtList:a ws '}'                      -> Expr.Block({}, [], a),

  arglist = argName:a (ws argName)*:as              -> [a].concat(as),

  replExpr = ws stmtList:xs ws eof -> Expr.Seq({}, xs),

  program = ws module:m ws eof    -> m
          | ws stmtList:xs ws eof -> Expr.Module({}, [], Expr.Record({}, []), xs)
}
