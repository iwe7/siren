// # module: mermaid.parser
//
// Parser for the Mermaid language.

// -- Dependencies -----------------------------------------------------
var ast = require('./ast');
var Expr = ast.Expr;
var Do = ast.DoClause;

var reserved = [
  'extend', 'let', 'this', 'do', 'module', 'exposing',
  '=', '>>', 'with', 'where', 'use', 'in', 'using',
  'true', 'false'
]

function isValid(a) {
  return reserved.indexOf(a) === -1
}

function get(k){ return function(o){ 
  return o[k] 
}}

function keywordApply(meta, target, args) {
  var id = Expr.Id(meta, args.map(get(0)).map(get('name')).join(''));
  return Expr.Apply(meta, id, target, args.map(get(1)))
}

function keywordLambda(meta, args, body, docs) {
  var id = Expr.Id(meta, args.map(get(0)).map(get('name')).join(''));
  return [id, Expr.Lambda(meta, args.map(get(1)), body, false, docs)]
}

// ## object: MermaidParser
ometa MermaidParser {
  space   = ^space | comment,
  comment = fromTo('//', '\n'),
  ws      = space*,
  symbol  = ~(letter | digit | space
             | '_' | '$' | '^' | '@' | ';' | ',' | '|' | ':' | '{' | '}' | '(' | ')' | '[' | ']') char,
  eof     = ~char,
  kw :xs  = seq(xs) ~(letter | digit | symbol | ':'),


  // Identifiers
  idStart    = letter,
  idRest     = letter | digit | symbol,
  identifier = <idStart idRest*>:a     ?(isValid(a)) -> Expr.Id({}, a),
  keyword    = <idStart idRest*>:a ':'               -> Expr.Id({}, a + ':'),
  symbols    = <symbol+>:a             ?(isValid(a)) -> Expr.Id({}, a),

  selector   = identifier
             | symbols
             | keyword+:xs -> Expr.Id(xs[0].meta, xs.map(get('name')).join('')),

  // Patterns
  pattern = keywordPattern
          | binaryPattern
          | unaryPattern,

  patternBody = '=' (ws string)?:a ws exprList:as -> [as, a? a.value : ''],

  keywordArgs = (ws keyword:k ws identifier:n -> [k, n])+,
  
  keywordPattern = keywordArgs:as ws patternBody:v            -> keywordLambda({}, as, v[0], v[1]),
  unaryPattern   = identifier:a ~':' ws patternBody:v         -> [a, Expr.Lambda({}, [], v[0], false, v[1])],
  binaryPattern  = symbols:a ws identifier:b ws patternBody:v -> [a, Expr.Lambda({}, [b], v[0], false, v[1])],

  

  // Statements
  letStmt = kw('let') ws identifier:a ws '=' ws expr:b -> Expr.Let({}, a, b),

  module     = kw('module') ws moduleArgs?:a ws exportStmt?:b ws kw('where') ws exprList:cs
             -> Expr.Module({}, a || [], b, cs),
  moduleArgs = '|' ws identifier:a (ws identifier)*:as ws '|' -> [a].concat(as),
  exportStmt = kw('exposing') ws expr:a -> a,

  useStmt = kw('use') ws listOf(#expr, ','):xs ws kw('in') ws '{' ws exprList:ys ws '}'
          -> Expr.Use({}, xs, ys),

  usingStmt = kw('using') ws listOf(#expr, ','):xs -> Expr.Using({}, xs),

  // Expressions
  expr = letStmt
       | usingStmt
       | useStmt
       | pipeExpr,

  pipeExpr = pipeExpr:a ws ">>" ws identifier:i ~':'        -> Expr.Apply({}, i, a, [])
           | pipeExpr:a ws ">>" ws symbols:s ws unaryExpr:b -> Expr.Apply({}, s, a, [b])
           | pipeExpr:a ws ">>" (ws keywordApp)+:as         -> keywordApply({}, a, as)
           | appExpr,
  
  appExpr = keywordExpr
          | binaryExpr,

  keywordApp  = keyword:k ws binaryExpr:v -> [k, v],

  keywordExpr = binaryExpr:a (ws keywordApp)+:as          -> keywordApply({}, a, as),
  binaryExpr  = binaryExpr:a ws symbols:s ws unaryExpr:b  -> Expr.Apply({}, s, a, [b])
              | unaryExpr,
  unaryExpr   = unaryExpr:a ws identifier:i ~':'      -> Expr.Apply({}, i, a, [])
              | unaryExpr:a ws record:v               -> Expr.Clone({}, a, v)
              | unaryExpr:a ':' ':' identifier:i ~':' -> Expr.Bind({}, a, i)
              | primaryExpr,
  primaryExpr = '_'                  -> Expr.Hole({})
              | doExpr
              | value
              | identifier:a ~':'    -> Expr.Var({}, a)
              | kw('this')           -> Expr.Self({})
              | extendExpr:a         -> a
              | ';'                  -> Expr.Empty
              | '(' ws expr:a ws ')' -> a,

  extendExpr = kw('extend') ws expr:a ws kw('with') ws record:v -> Expr.Extend({}, a, v),

  exprList    = expr:a (ws ';' ws expr)*:as -> [a].concat(as),

  doExpr = kw('do') ws '{' ws (doCase:c ws ';' ws -> c)*:xs ws doTailCase:x ws '}'
         -> Expr.Do({}, xs.concat([x])),
  doAction     = identifier:i ws "<-" ws expr:e -> Do.Action({}, i, e)
               | doTailAction,
  doTailAction = expr:e                         -> Do.Action({}, Expr.Id({}, '_'), e),
  doPure       = identifier:i ws "<-" ws kw('return') ws expr:e -> Do.Return({}, i, e)
               | doTailPure,
  doTailPure   = kw('return') ws expr:e                         -> Do.Return({},
                                                                             Expr.Id({}, '_'),
                                                                             e),
  doCase       = doPure | doAction,
  doTailCase   = doTailPure | doTailAction,

  // Values
  value = bool | number | string | vector | lambda | record,

  bool = kw('true') -> Expr.Bool({}, true)
       | kw('false') -> Expr.Bool({}, false),
  
  octDigit = /[0-7_]/,
  hexDigit = /[0-9a-fA-F_]/,
  binDigit = '0' | '1' | '_',
  digits = (digit | '_')+:as -> Number(as.join('').replace(/_/g, '')),
  number = '-'?:s ( '0' 'b' <binDigit+>:n
                    -> Expr.Num({}, +((s || '+') + parseInt(n.replace(/_/g, ''), 2)))
                  | '0' 'o' <octDigit+>:n
                    -> Expr.Num({}, +((s || '+') + parseInt(n.replace(/_/g, ''), 8)))
                  | '0' 'x' <hexDigit+>:n
                    -> Expr.Num({}, +((s || '+') + parseInt(n.replace(/_/g, ''), 16)))
                  | digits:a ('.' digits:b exponent:c)?
                    -> Expr.Num({}, +((s || '+') + a + '.' + (b || 0) + (c || 0)))
                  ),
  sign = '+' | '-',
  exponent = ('e' | 'E') sign?:s digits:d -> s + d,
  
  stringEscape     = '\\' ( 'b' -> '\b'
                          | 'f' -> '\f'
                          | 'n' -> '\n'
                          | 'r' -> '\r'
                          | 't' -> '\t'
                          | char),
  stringChar       = (stringEscape | (~seq('"') char)):a -> a,
  unescapableChars = ~seq('"""') char,
  string           = seq('"""') <unescapableChars*>:as seq('"""') -> Expr.Str({}, as)
                   | '"' (stringChar)*:as '"'                     -> Expr.Str({}, as.join('')),

  vector = '[' ws expr:hd (ws ',' ws expr)*:tl ws ']' -> Expr.Vector({}, [hd].concat(tl))
         | '[' ws ']'                                 -> Expr.Vector({}, []),
  
  record = '{' ws pattern:hd (ws ',' ws pattern)*:tl ws '}' -> Expr.Record({}, [hd].concat(tl))
         | '{' ws '}'                                       -> Expr.Record({}, []),

  lambda  = '{' ws arglist:as ws '|' ws exprList:a ws '}' -> Expr.Lambda({}, as, a, true, '')
          | '{' ws exprList:a ws '}'                      -> Expr.Lambda({}, [], a, true, ''),
  
  arglist = identifier:a (ws identifier)*:as              -> [a].concat(as),

  program = ws module:m ws eof    -> m
          | ws exprList:xs ws eof -> Expr.Program(xs)
}
